plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'org.jwildfire'
version = '9.02-SNAPSHOT'

repositories {
    mavenCentral()
    flatDir { dirs 'lib' }
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

sourceSets {
    main {
        java { srcDir 'src' }
        resources { srcDir 'resources' }
    }
    test {
        java { srcDir 'test/src' }
    }
}

application {
    mainClass = 'org.jwildfire.swing.JWildfire'
}

ext {
    javaFxVersion = '21.0.2'
    lwjglVersion = '3.3.3'
}

javafx {
    version = javaFxVersion
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.graphics', 'javafx.web' ]
}

dependencies {
    // Core Dependencies
    implementation 'commons-collections:commons-collections:3.2.2'
    implementation 'com.kitfox.svg:svg-salamander:1.0'
    implementation 'colt:colt:1.2.0'
    implementation 'javazoom:jlayer:1.0.1'
    implementation 'edu.emory.mathcs:JTransforms:2.4'
    implementation 'org.scijava:jep:2.4.2'
    implementation 'com.jtattoo:JTattoo:1.6.11'
    implementation 'commons-io:commons-io:2.14.0'

    // Dependencies from lib folder (Legacy/Not in Maven Central)
    implementation fileTree(dir: 'lib', include: ['*.jar'], exclude: ['lwjgl.jar', 'junit*.jar', 'commons-io*.jar'])

    // LWJGL
    implementation platform("org.lwjgl:lwjgl-bom:${lwjglVersion}")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-opencl"

    // ControlsFX
    implementation 'org.controlsfx:controlsfx:11.2.1'

    // LWJGL Natives (Linux)
    runtimeOnly "org.lwjgl:lwjgl::natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-glfw::natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-opengl::natives-linux"

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 21
}

tasks.withType(Tar) { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }
tasks.withType(Zip) { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

run {
    systemProperty "java.library.path", "lib"
}

shadowJar {
    mergeServiceFiles()
}
